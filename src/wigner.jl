using Mmap

export
    wigner,
    WignerFunction

#=
    Wigner function by Laguerre Polynominal
=#

function wigner(m::Integer, n::Integer, x::Real, p::Real)
    w = gaussian_function(x, p)
    w *= coefficient_of_wave_function(m, n)
    w *= z_to_power(m, n, x, p)
    w *= laguerre(m, n, x, p)

    return w
end

wigner(m::Integer, n::Integer) = (x::Real, p::Real)->wigner(m, n, x, p)

function create_wigner(
    m_dim::Integer,
    n_dim::Integer,
    x_range::AbstractRange,
    p_range::AbstractRange
)
    ğ° = Array{ComplexF64,4}(undef, m_dim, n_dim, length(x_range), length(p_range))
    @sync for m in 1:m_dim
        for n in 1:n_dim
            for (x_i, x) in enumerate(x_range)
                Threads.@spawn for (p_j, p) in enumerate(p_range)
                    ğ°[m, n, x_i, p_j] = wigner(m ,n, x, p)
                end
            end
        end
    end

    path = datadep"SqState"
    bin_path = joinpath(
        path,
        "W m=$(m_dim) n=$(n_dim) x=$(replace(string(x_range), ":" => "_")) p=$(replace(string(p_range), ":" => "_")).bin"
    )
    save_ğ°(bin_path, ğ°)

    return ğ°
end

mutable struct WignerFunction{T<:Integer, U<:AbstractRange}
    m_dim::T
    n_dim::T
    x_range::U
    p_range::U
    ğ°::Array{ComplexF64,4}

    function WignerFunction(
        m_dim::T,
        n_dim::T,
        x_range::U,
        p_range::U
    ) where {T<:Integer, U<:AbstractRange}
        !check_argv(m_dim, n_dim, x_range, p_range) && throw(ArgumentError)

        # load from Mmap
        path = datadep"SqState"
        bin_path = joinpath(
            path,
            "W m=$(m_dim) n=$(n_dim) x=$(replace(string(x_range), ":" => "_")) p=$(replace(string(p_range), ":" => "_")).bin"
        )
        if isfile(bin_path)
            ğ° = load_ğ°(m_dim, n_dim, x_range, p_range, bin_path)
            return new{T, U}(m_dim, n_dim, x_range, p_range, ğ°)
        end

        ğ° = create_wigner(m_dim, n_dim, x_range, p_range)
        return new{T, U}(m_dim, n_dim, x_range, p_range, ğ°)
    end
end

function WignerFunction(x_range::AbstractRange, p_range::AbstractRange; dim=35)
    return WignerFunction(dim, dim, x_range, p_range)
end

function (wf::WignerFunction)(Ï::AbstractMatrix)
    ğ°_surface = Matrix{Float64}(undef, length(wf.x_range), length(wf.p_range))
    @sync for i in 1:length(wf.x_range)
        Threads.@spawn for j in 1:length(wf.p_range)
            ğ°_surface[i, j] = real(sum(Ï .* wf.ğ°[:, :, i, j]))
        end
    end

    return ğ°_surface
end

function save_ğ°(bin_path::String, ğ°::Array{ComplexF64,4})
    @info "Save W_{m,n,x,p} to $bin_path"
    mem = open(bin_path, "w+")
    write(mem, ğ°)
    close(mem)
end

function load_ğ°(
    m_dim::Integer,
    n_dim::Integer,
    x_range::AbstractRange,
    p_range::AbstractRange,
    bin_path::String
)
    @info "Load W_{m,n,x,p} from $bin_path"
    mem = open(bin_path)
    ğ° = Mmap.mmap(
        mem,
        Array{ComplexF64,4},
        (m_dim, n_dim, length(x_range), length(p_range))
    )
    close(mem)

    return ğ°
end

check_zero(m_dim, n_dim) = !iszero(m_dim) && !iszero(n_dim)

check_empty(x_range, p_range) = !isempty(x_range) && !isempty(p_range)

function check_argv(m_dim, n_dim, x_range, p_range)
    return check_zero(m_dim, n_dim) && check_empty(x_range, p_range)
end
